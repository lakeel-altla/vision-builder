apply plugin: 'com.android.application'

// retrolambda
apply plugin: 'me.tatarka.retrolambda'

// to use Google API.
Properties googleApiProperties = new Properties()
googleApiProperties.load(project.rootProject.file('google-api.properties').newDataInputStream())

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.3"

    defaultConfig {
        applicationId "com.lakeel.altla.vision.builder"
        minSdkVersion 19
        targetSdkVersion 25
        versionCode 1
        versionName "1.0"
        multiDexEnabled true

        try {
            manifestPlaceholders = [GOOGLE_API_KEY: googleApiProperties.getProperty("GOOGLE_API_KEY")]
        } catch (e) {
            throw new RuntimeException("GOOGLE_API_KEY in google-api.properties is required.")
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

configurations { natives }

dependencies {
    compile 'com.android.support:multidex:1.0.1'
    compile 'com.android.support:appcompat-v7:25.3.1'
    compile 'com.android.support:design:25.3.1'
    compile 'com.android.support:percent:25.3.1'
    compile 'com.afollestad.material-dialogs:core:0.9.4.4'
    compile 'com.squareup.picasso:picasso:2.5.2'
    compile "io.reactivex.rxjava2:rxjava:2.0.4"
    compile 'io.reactivex.rxjava2:rxandroid:2.0.1'
    compile 'org.greenrobot:eventbus:3.0.0'

    compile "com.badlogicgames.gdx:gdx-backend-android:$gdxVersion"
    natives "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-armeabi"
    natives "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-armeabi-v7a"
    natives "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-arm64-v8a"
    natives "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-x86"
    natives "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-x86_64"
    compile "com.badlogicgames.gdx:gdx:$gdxVersion"

    compile 'com.google.dagger:dagger:2.10'
    annotationProcessor 'com.google.dagger:dagger-compiler:2.10'

    compile 'com.jakewharton:butterknife:8.5.1'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.5.1'

    compile 'org.parceler:parceler-api:1.1.6'
    annotationProcessor 'org.parceler:parceler:1.1.6'

    compile project(path: ':android-binding')

    debugCompile project(path: ':vision-core', configuration: 'debug')
    debugCompile 'com.squareup.leakcanary:leakcanary-android:1.4'

    releaseCompile project(path: ':vision-core', configuration: 'debug')
    releaseCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.4'
}

// google-services plugin to use firebase.
apply plugin: 'com.google.gms.google-services'

// called every time gradle gets executed, takes the native dependencies of
// the natives configuration, and extracts them to the proper libs/ folders
// so they get packed with the APK.
task copyAndroidNatives() {
    file("src/main/jniLibs/armeabi/").mkdirs();
    file("src/main/jniLibs/armeabi-v7a/").mkdirs();
    file("src/main/jniLibs/arm64-v8a/").mkdirs();
    file("src/main/jniLibs/x86_64/").mkdirs();
    file("src/main/jniLibs/x86/").mkdirs();

    configurations.natives.files.each { jar ->
        def outputDir = null
        if (jar.name.endsWith("natives-arm64-v8a.jar")) outputDir = file("src/main/jniLibs/arm64-v8a")
        if (jar.name.endsWith("natives-armeabi-v7a.jar")) outputDir = file("src/main/jniLibs/armeabi-v7a")
        if (jar.name.endsWith("natives-armeabi.jar")) outputDir = file("src/main/jniLibs/armeabi")
        if (jar.name.endsWith("natives-x86_64.jar")) outputDir = file("src/main/jniLibs/x86_64")
        if (jar.name.endsWith("natives-x86.jar")) outputDir = file("src/main/jniLibs/x86")
        if (outputDir != null) {
            copy {
                from zipTree(jar)
                into outputDir
                include "*.so"
            }
        }
    }
}
